<програма>  ::=  tiger id { <список_ог1> start <список_операторів1> finish } 
<список_ог1> ::= <список_ог.>
<список_ог.> ::= <оголошення> ¶|<список_ог.> <оголошення> ¶
<оголошення> ::= <тип> <сп.ід1>
<тип> ::= int|float
<сп.ід1> ::= <сп.ід.>
<сп.ід.> ::= id|<сп.ід.> , id
<список_операторів1> ::= <список_операторів>
<список_операторів> ::= <оператор>|label : <оператор>|<список_операторів> ¶ <оператор>|<список_операторів> ¶ label : <оператор>
<оператор> ::= <присвоювання>|<введення>|<виведення>|<цикл>|<умовний_перехід>
<присвоювання> ::= id = <вираз1>
<вираз1> ::= <вираз>
<вираз> ::= <терм1>|<вираз> + <терм1>|<вираз> - <терм1>
<терм1> ::= <терм>
<терм> ::= <множ.>|<терм> * <множ.>|<терм> / <множ.>
<множ.> ::= ( <вираз1> )|id ;|const
<введення> ::= <введення> >> id|cin >> id
<виведення> ::= <виведення> << id|echo << id
<цикл> ::= repeat <список_операторів1> until <лог.вираз1>
<лог.вираз1> ::= <лог.вираз>
<лог.вираз> ::= <LT1>|<лог.вираз> or <LT1>
<LT1> ::= <LT>
<LT> ::= <LM>|<LT> and <LM>
<LM> ::= <LR>|not <LM>|[ <лог.вираз1> ]
<LR> ::= <вираз> <знак_відн.> <вираз1>
<знак_відн.> ::= >|<|>=|<=|==|!=
<умовний_перехід> ::= if <лог.вираз1> goto label
